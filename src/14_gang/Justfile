build_path    := "../../build/14_gang/"

grub_cfg      := "grub.cfg"
linker_script := "linker.ld"
target        := "x86_64-os"
iso           := build_path + "os-x86_64.iso"
kernel        := build_path + "kernel-x86_64.bin"
rust_os       := "target/x86_64-os/debug/libos.a"
asm           := "src/asm/boot.asm"
asm_obj       := build_path + "asm/boot.o"
iso_path      := build_path + "iso"

default: run

@build_kernel:
    cargo build -Zbuild-std --target {{target}}.json
    mkdir -p {{build_path + "asm"}}
    nasm -felf64 {{asm}} -o {{asm_obj}}
    ld -n --gc-sections -T {{linker_script}} -o {{kernel}} {{asm_obj}} {{rust_os}} 2>/dev/null

@build: build_kernel
    mkdir -p {{build_path + "iso/boot/grub"}}
    cp {{kernel}} {{build_path + "iso/boot/kernel.bin"}}
    cp {{grub_cfg}} {{build_path + "iso/boot/grub"}}
    grub-mkrescue -o {{iso}} {{iso_path}} 2>/dev/null
    rm -r {{iso_path}}

@run: build
    echo "Running..."
    qemu-system-x86_64 -cdrom {{iso}} -audiodev pa,id=speaker -machine pcspk-audiodev=speaker -m 32M

@clean:
    rm -r build